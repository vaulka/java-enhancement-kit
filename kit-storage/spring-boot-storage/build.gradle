plugins {
    id "signing"
    id "java-library"
    id "maven-publish"
    id "org.springframework.boot" version "$springBootVersion"
    id "io.spring.dependency-management" version "$springDependencyManagementVersion"
}

jar {
    enabled = true
}

bootJar {
    enabled = false
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    classifier "javadoc"
}

publishing {
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ""
                password ""
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId group.toString()
            artifactId "spring-boot-storage"
            version version.toString()
            description "Java 增强套件 之 云存储 Spring Boot 模块"

            pom {
                name = "java-enhancement-kit"
                description = "Java 增强套件 之 云存储 Spring Boot 模块"
                url = "https://github.com/JarvisPongSky/java-enhancement-kit"
                licenses {
                    license {
                        name = "GPL-v3.0 协议"
                        url = "https://www.gnu.org/licenses/gpl-3.0-standalone.html"
                    }
                }
                developers {
                    developer {
                        id = "PONGSKY"
                        name = "PONGSKY"
                        email = "kelry@vip.qq.com"
                    }
                }
                scm {
                    url = "https://github.com/JarvisPongSky/java-enhancement-kit"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    api project(":storage:storage-annotation")
    api project(":type-parser:type-parser-utils")

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    api "org.openjdk.jol:jol-core:$jolCoreVersion"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    api "org.springframework.boot:spring-boot-starter-web"
    api "org.springframework.boot:spring-boot-starter-aop"
    api "org.springframework.boot:spring-boot-starter-validation"
}